// no-core
import { Optional } from ./Optional.tlk
import { Array } from ./Array.tlk
import { Add, Comparable } from ./Operators.tlk

public protocol Iterator {
	associated Element
	func next() -> Optional<Element>
}

public protocol Iterable {
	associated Element
	associated T: Iterator
	func iter() -> T
}

public struct ArrayIterator<Element> {
	let array: Array<Element>
	let current: Int

	init(array: Array<Element>) {
		self.array = array
		self.current = 0
	}

	extend Self: Iterator<Element> {
		func next() -> Optional<Element> {
			if self.current < self.array.count {
				let result = self.array.get(self.current)
				self.current = self.current + 1
				Optional.some(result)
			} else {
				Optional.none
			}
		}
	}
}

extend Array<Element>: Iterable {
	func iter() -> ArrayIterator<Element> {
		ArrayIterator<Element>(array: self)
	}
}
